module Bertot.Lang where

open import Prelude
open import Data.Empty
open import Data.Bool
open import Data.Nat
open import Data.String
open import Data.List
open import Data.Dec renaming (elim to elimáµˆ)

{- The programming language -}

data AExpr : ğ’° where
  ANum  : â„• â†’ AExpr
  AVar  : String â†’ AExpr
  APlus : AExpr â†’ AExpr â†’ AExpr

data BExpr : ğ’° where
  BLt : AExpr â†’ AExpr â†’ BExpr

data Instr : ğ’° where
  Assign : String â†’ AExpr â†’ Instr
  Seq    : Instr â†’ Instr â†’ Instr
  While  : BExpr â†’ Instr â†’ Instr

af : (String â†’ â„•) â†’ AExpr â†’ â„•
af g (ANum n)      = n
af g (AVar x)      = g x
af g (APlus eâ‚ eâ‚‚) = af g eâ‚ + af g eâ‚‚

bf : (String â†’ â„•) â†’ BExpr â†’ Bool
bf g (BLt eâ‚ eâ‚‚) = af g eâ‚ <áµ‡ af g eâ‚‚

{- Weakest pre-condition calculus -}

data Assert : ğ’° where
  QPred  : String â†’ List AExpr â†’ Assert
  QB     : BExpr â†’ Assert
  QConj  : Assert â†’ Assert â†’ Assert
  QNot   : Assert â†’ Assert
  QTrue  : Assert
  QFalse : Assert

{- computation of the property associated to an assertion -}
ia : (String â†’ List â„• â†’ ğ’°) â†’ (String â†’ â„•) â†’ Assert â†’ ğ’°
ia m g (QPred s l)   = m s (map (af g) l)
ia m g (QB b)        = is-true (bf g b)
ia m g (QConj aâ‚ aâ‚‚) = ia m g aâ‚ Ã— ia m g aâ‚‚
ia m g (QNot a)      = Â¬ (ia m g a)
ia m g  QTrue        = âŠ¤
ia m g  QFalse       = âŠ¥

ia-prop : {m : String â†’ List â„• â†’ ğ’°} {g : String â†’ â„•} (a : Assert)
        â†’ ((s : String) â†’ (l : List â„•) â†’ is-prop (m s l))
        â†’ is-prop (ia m g a)
ia-prop {g} (QPred s l)   m-prop = m-prop s (map (af g) l)
ia-prop     (QB b)        _      = hlevel 1
ia-prop     (QConj aâ‚ aâ‚‚) m-prop = Ã—-is-of-hlevel 1 (ia-prop aâ‚ m-prop) (ia-prop aâ‚‚ m-prop)
ia-prop     (QNot a)      _      = hlevel 1
ia-prop      QTrue        _      = hlevel 1
ia-prop      QFalse       _      = hlevel 1

{- Annotated instruction -}

data AnInstr : ğ’° where
  AnPre    : Assert â†’ AnInstr â†’ AnInstr
  AnAssign : String â†’ AExpr â†’ AnInstr
  AnSeq    : AnInstr â†’ AnInstr â†’ AnInstr
  AnWhile  : BExpr â†’ Assert â†’ AnInstr â†’ AnInstr

cleanup : AnInstr â†’ Instr
cleanup (AnPre _ i)     = cleanup i
cleanup (AnAssign x e)  = Assign x e
cleanup (AnSeq iâ‚ iâ‚‚)   = Seq (cleanup iâ‚) (cleanup iâ‚‚)
cleanup (AnWhile b a i) = While b (cleanup i)

{- computation of the pre-condition for an annotated instruction and a post-condition -}

asubst : String â†’ AExpr â†’ AExpr â†’ AExpr
asubst _ _ e@(ANum _)      = e
asubst x s e@(AVar y)      = if âŒŠ x â‰Ÿ y âŒ‹ then s else e
asubst x s   (APlus eâ‚ eâ‚‚) = APlus (asubst x s eâ‚) (asubst x s eâ‚‚)

bsubst : String â†’ AExpr â†’ BExpr â†’ BExpr
bsubst x s (BLt eâ‚ eâ‚‚) = BLt (asubst x s eâ‚) (asubst x s eâ‚‚)

qsubst : String â†’ AExpr â†’ Assert â†’ Assert
qsubst x s (QPred p l)   = QPred p (map (asubst x s) l)
qsubst x s (QB b)        = QB (bsubst x s b)
qsubst x s (QConj aâ‚ aâ‚‚) = QConj (qsubst x s aâ‚) (qsubst x s aâ‚‚)
qsubst x s (QNot a)      = QNot (qsubst x s a)
qsubst _ _  QTrue        = QTrue
qsubst _ _  QFalse       = QFalse

pc : AnInstr â†’ Assert â†’ Assert
pc (AnPre a _)     _    = a
pc (AnAssign x e)  post = qsubst x e post
pc (AnSeq iâ‚ iâ‚‚)   post = pc iâ‚ (pc iâ‚‚ post)
pc (AnWhile _ a _) _    = a

{- A verification condition generator -}

record Cond : ğ’° where
  constructor imp
  field
    câ‚ : Assert
    câ‚‚ : Assert

open Cond

vc : AnInstr â†’ Assert â†’ List Cond
vc (AnPre a i)     post = imp a (pc i post) âˆ· vc i post
vc (AnAssign _ _)  _    = []
vc (AnSeq iâ‚ iâ‚‚)   post = vc iâ‚ (pc iâ‚‚ post) ++ vc iâ‚‚ post
vc (AnWhile b a i) post = imp (QConj a (QB b)) (pc i a) âˆ· imp (QConj a (QNot (QB b))) post âˆ· vc i a

-- TODO rewrite via All?
valid : (String â†’ List â„• â†’ ğ’°) â†’ List Cond â†’ ğ’°
valid m []      = âŠ¤
valid m (h âˆ· t) = (âˆ€ (g : String â†’ â„•) â†’ ia m g (h .câ‚) â†’ ia m g (h .câ‚‚)) Ã— valid m t

valid-prop : {m : String â†’ List â„• â†’ ğ’°} â†’ (l : List Cond)
           â†’ ((s : String) â†’ (l : List â„•) â†’ is-prop (m s l))
           â†’ is-prop (valid m l)
valid-prop []      _      = hlevel 1
valid-prop (h âˆ· l) m-prop = Ã—-is-of-hlevel 1
                              (Î -is-of-hlevel 1 Î» g â†’ fun-is-of-hlevel 1 (ia-prop (h .câ‚‚) m-prop))
                              (valid-prop l m-prop)

{- A monotonicity property -}

subst-sound-a : {g : String â†’ â„•} {eâ‚€ : AExpr} {x : String}
              â†’ (e : AExpr)
              â†’ af g (asubst x eâ‚€ e) ï¼ af (Î» y â†’ if âŒŠ x â‰Ÿ y âŒ‹ then af g eâ‚€ else g y) e
subst-sound-a              (ANum n)      = refl
subst-sound-a {g} {eâ‚€} {x} (AVar y)      =
  elimáµˆ {C = Î» q â†’ af g (if âŒŠ q âŒ‹ then eâ‚€ else AVar y) ï¼ (if âŒŠ q âŒ‹ then af g eâ‚€ else g y)}
        (Î» _ â†’ refl)
        (Î» _ â†’ refl)
        (x â‰Ÿ y)
subst-sound-a              (APlus eâ‚ eâ‚‚) =
  apÂ² _+_ (subst-sound-a eâ‚) (subst-sound-a eâ‚‚)

subst-sound-b : {g : String â†’ â„•} {e : AExpr} {x : String}
              â†’ (b : BExpr)
              â†’ bf g (bsubst x e b) ï¼ bf (Î» y â†’ if âŒŠ x â‰Ÿ y âŒ‹ then af g e else g y) b
subst-sound-b (BLt eâ‚ eâ‚‚) = apÂ² _<áµ‡_ (subst-sound-a eâ‚) (subst-sound-a eâ‚‚)

subst-sound-l : {g : String â†’ â„•} {e : AExpr} {x : String}
              â†’ (l : List AExpr)
              â†’ map (af g) (map (asubst x e) l) ï¼ map (af (Î» y â†’ if âŒŠ x â‰Ÿ y âŒ‹ then af g e else g y)) l
subst-sound-l []      = refl
subst-sound-l (h âˆ· t) = apÂ² _âˆ·_ (subst-sound-a h) (subst-sound-l t)

subst-sound : {m : String â†’ List â„• â†’ ğ’°} {g : String â†’ â„•} {e : AExpr} {x : String}
            â†’ (a : Assert)
            â†’ ia m g (qsubst x e a) ï¼ ia m (Î» y â†’ if âŒŠ x â‰Ÿ y âŒ‹ then af g e else g y) a
subst-sound {m} (QPred s l)   = ap (m s) (subst-sound-l l)
subst-sound     (QB b)        = ap is-true (subst-sound-b b)
subst-sound     (QConj aâ‚ aâ‚‚) = apÂ² _Ã—_ (subst-sound aâ‚) (subst-sound aâ‚‚)
subst-sound     (QNot a)      = ap Â¬_ (subst-sound a)
subst-sound      QTrue        = refl
subst-sound      QFalse       = refl

valid-cat : âˆ€ {m l2} (l1 : List Cond)
          â†’ valid m l1 â†’ valid m l2 â†’ valid m (l1 ++ l2)
valid-cat []             v1  v2 = v2
valid-cat (x âˆ· l1) (vx , v1) v2 = vx , valid-cat l1 v1 v2

valid-cat-inv : âˆ€ {m l2} (l1 : List Cond)
              â†’ valid m (l1 ++ l2)
              â†’ valid m l1 Ã— valid m l2
valid-cat-inv []       vc        = tt , vc
valid-cat-inv (x âˆ· l1) (vx , vc) =
  let (ih1 , ih2) = valid-cat-inv l1 vc in
  (vx , ih1) , ih2

vc-monotonic : âˆ€ {m p1 p2} â†’ (i : AnInstr)
             â†’ valid m (vc i p1) â†’ (âˆ€ g â†’ ia m g p1 â†’ ia m g p2)
             â†’ valid m (vc i p2) Ã— (âˆ€ g â†’ ia m g (pc i p1) â†’ ia m g (pc i p2))
vc-monotonic           (AnPre a i)    (v12 , vc)       p12 =
  let (ih1 , ih2) = vc-monotonic i vc p12 in
  ((Î» g â†’ ih2 g âˆ˜ v12 g) , ih1) , Î» g â†’ id
vc-monotonic {p1} {p2} (AnAssign x e)  tt              p12 =
  tt , Î» g z â†’ transport (subst-sound p2 â»Â¹) (p12 (Î» y â†’ if âŒŠ x â‰Ÿ y âŒ‹ then af g e else g y) (transport (subst-sound p1) z))
vc-monotonic {p1} {p2} (AnSeq iâ‚ iâ‚‚)   v               p12 =
  let (v1   , v2)   = valid-cat-inv (vc iâ‚ (pc iâ‚‚ p1)) v
      (ih21 , ih22) = vc-monotonic iâ‚‚ v2 p12
      (ih11 , ih12) = vc-monotonic {p1 = pc iâ‚‚ p1} iâ‚ v1 ih22
    in
  valid-cat (vc iâ‚ (pc iâ‚‚ p2)) ih11 ih21 , ih12
vc-monotonic           (AnWhile b a i) (v12 , vn , vc) p12 =
  (v12 , (Î» g â†’ p12 g âˆ˜ vn g) , vc) , Î» g â†’ id
